// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   Mux16(a[0..15]=x[0..15], b[0..15]=false, sel=zx, out=zx16);
   Not16(in=zx16, out=nzx16);
   Mux16(a=zx16, b=nzx16, sel=nx, out=zx16ORnzx16);
   
   Mux16(a[0..15]=y[0..15], b[0..15]=false, sel=zy, out=zy16);
   Not16(in=zy16, out=nzy16);
   Mux16(a=zy16, b=nzy16, sel=ny, out=zy16ORnzy16);
   
   Add16(a=zx16ORnzx16, b=zy16ORnzy16, out=xADDy);
   And16(a=zx16ORnzx16, b=zy16ORnzy16, out=xANDy);
   
   Mux16(a=xANDy, b=xADDy, sel=f, out=andORadd);
   Not16(in=andORadd, out=NandORadd);
   Mux16(a=andORadd, b=NandORadd, sel=no, out[0..7]=lowOut, out[8..15]=highOut, out[15]=ng, out=out);
   
   Or8Way(in=lowOut, out=lowOR);
   Or8Way(in=highOut, out=highOR);
   
   Or(a=lowOR, b=highOR, out=zrNext);
   Not(in=zrNext, out=zr);
   
}